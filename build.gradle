buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "11.1"
group= "mod.chiselsandbits"
archivesBaseName = "chiselsandbits"

repositories {
	maven { url "http://dvs1.progwml6.com/files/maven" }
	maven { url "http://maven.amadornes.com/" }
	flatDir { dirs 'libs' }
}

dependencies {
	deobfCompile "mezz.jei:jei_1.9.4:3.4.0.204"
	deobfCompile "MCMultiPart:MCMultiPart-experimental:1.2.0_68:universal"
}

minecraft {
    version = "1.10-12.18.0.1983-1.10.0"
    runDir = "eclipse"
    mappings = "snapshot_20160518"
    replaceIn "ChiselsAndBits.java"
    replace "@VERSION@", project.version
}

task apiJar(type: Jar) {
    from sourceSets.main.java
    include "mod/chiselsandbits/api/**"

    from sourceSets.main.output
    include "mod/chiselsandbits/api/**"

    classifier = 'api'
}

jar {
	manifest {
		attributes 'FMLAT': 'chiselsandbits_at.cfg'
	}
}

artifacts {
    archives apiJar
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    
    // Move access transformers to META-INF
    rename '(.+_at.cfg)', 'META-INF/$1'
}
